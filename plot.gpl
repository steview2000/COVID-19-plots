#!/usr/bin/gnuplot -persist
load 'beauty.gnu'
@sep ","

set timefmt "%m/%d/%y"
set xdata time

dinc(x) = (vD = (x-old), old = x, vD)
av7(x) = (s7 = (x1+x2+x3+x4+x5+x6+x)/7,x6=x5,x5=x4,x4=x3,x3=x2,x2=x1,x1=x,s7)

#deriv1(x,y) = (vD1 = 2*(y-old_y1)/((x-old_x1)*(old_y1+y)), old_y1 = y, old_x1 = x, vD1)
x1 = x2 =x3 =x4 =x5 =x6 =x7=0.0
y1 = y2 =y3 =y4 =y5 =y6 =x7=0.0

old=0.0

set xrange [*:*]
set yrange [*:*]

set term post eps enhanced color size 4.5,7
set out "plot.eps"

set multiplot
ROW_Number=2
COL_Number=1
h_gap=0.1
left_margin=0.14
right_margin=0.03
top_margin=0.05
load 'multi.gpl'
set label 1 "infections" at screen 0.4,0.97 font ",20"
set label 2 "vaccination in Germany" at screen 0.2,0.38 font ",20"
set xlabel ""
set ylabel "weekly cases per 100,000 inhabitants"
set xrange ["3/01/20":*]
set key top left
set xtics autofreq rotate by 45 offset -3,-3
set yrange [0:*]
############### Infections #######################################
@MARGINS_1
p 'data/germany.csv' u 1:(7*av7(dinc($2))/(10*$5)) title "Germany" w l lt 1 lc "red",\
  'data/israel.csv'  u 1:(7*av7(dinc($2))/(10*$5)) title "Israel" w l lt 1 lc "blue",\
  'data/sweden.csv'  u 1:(7*av7(dinc($2))/(10*$5)) title "Sweden" w l lt 1 lc "green",\
  'data/spain.csv'   u 1:(7*av7(dinc($2))/(10*$5)) title "Spain" w l lt 1 lc "black"

############### Impfzahlen #######################################
set timefmt "%Y-%m-%d"
set xrange [*:*]
set ylabel "daily vacination"
#set boxwidth 0.9
set style fill solid 0.3
@MARGINS_2
p 'data/impfzahlen.csv' u 1:($2+$3) title "1st vaccination" w boxes lc "green",\
                  '' u 1:($3) title "1st+2nd vaccination" w boxes lc "blue"

unset multiplot
set out 
reset
load '~/.gnuplot'
@sep ','
set term x11
set xdata
set yrange [0:*]
set bmargin at screen 0.1
set tmargin -1
set lmargin -1
set rmargin -1
set ytics nomirror
set y2tics autofreq
set y2range [0:100]
p 'data/germany.csv' u 0:(av7(dinc($2))) axis x1y1 title "Germany" w l lt 1 lc "red",\
  'data/impfzahlen.csv' u (339+$0+17):(0.0683*av7($4)) axis x1y1 title "1st vaccination" w l lc "green"

set term x11
